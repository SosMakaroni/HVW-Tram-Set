// --------------------------------------------------------------------------------UV11

spriteset(ss_UV11_purchase, "src/UV/UV11_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_UV11_PDO_purchase, "src/UV/UV11_BUY_PDO_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_UV11_1, "src/UV/UV1_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV11_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV11_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV11_1i, "src/UV/UV1_01_8bpp.png")		{tmpl_depo()}
spriteset(ss_UV11_1_PUP, "src/UV/UV1_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV11_1_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV11_1_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV11_1_PUPi, "src/UV/UV1_01_PUP_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_UV11_1 {	loaded:  [ss_UV11_1];	loading: [ss_UV11_1];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_UV11_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV11_purchase;    // Alap modell
    1: return ss_UV11_PDO_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, swg_UV11_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV11_1i;    // Alap modell
    1: return ss_UV11_1_PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_UV11i, position_in_consist ) {
    1: ss_toldat;
    swg_UV11_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, swg_UV11_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV11_1;    // Alap modell
    1: return ss_UV11_1_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_UV11, position_in_consist ) {
    1: ss_toldat;
    swg_UV11_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_UV11, (extra_callback_info1 & 0x12)) {
   0x10: sw1_UV11i; // Depó nézet
   0x11: sw1_UV11i; // Jármű infó/Refit nézet
   0x12: sw1_UV11i; // Jármű lista nézet
   0x00: sw1_UV11; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_UV11_articulated, extra_callback_info1) {
    0: item_UV11;
    1: item_UV11_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_UV11_length, position_in_consist) {
	1: return 8;
    return 7;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_UV11_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_UV11_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_UV11_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_UV11_INFO); // Jármű infóban név
    0x20: sw_UV11_namep; // Vásárlási lista név
	0x21: return string(STR_UV11_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_UV11) {
	property {
        name:								string(STR_UV_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1956,04,03);
        model_life:						20;
        vehicle_life:						30;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					30;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								60 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								148 kW;
        weight:							20 ton;
        cargo_capacity:					94;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
//		variant_group:					item_UV11;
    }
    graphics {
		default:                 		sw_UV11;
		purchase:						swg_UV11_purchase;
		articulated_part:        		sw_UV11_articulated;
		length:                  		sw_UV11_length;
		cost_factor:					20 * parapuco;
		running_cost_factor:			22 * pararuco;
		additional_text: 				string(STR_UV11_ADAT);
		name:							sw_UV11_name;
    }
}
item(FEAT_ROADVEHS, item_UV11_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_UV11_length;
    }
}

// --------------------------------------------------------------------------------UV11_01 End
// --
// --
