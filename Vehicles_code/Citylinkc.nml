// --------------------------------------------------------------------------------Citylinkc

spriteset(ss_Citylinkc, "src/CityLink/CLc_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_Citylinkc_PDO, "src/CityLink/CL_BUY_PDO_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_Citylinkc_1, "src/CityLink/CLc_1_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylinkc_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLink/CLc_1_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylinkc_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLink/CLc_1_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_Citylinkc_1i, "src/CityLink/CLc_1_8bpp.png")		{tmpl_depo()}
spriteset(ss_Citylinkc_2, "src/CityLink/CLc_2_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylinkc_2, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylinkc_2, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_Citylinkc_2i, "src/CityLink/CLc_2_8bpp.png")		{tmpl_depo()}
spriteset(ss_Citylinkc_2_PUP, "src/CityLink/CLc_2_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylinkc_2_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylinkc_2_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_Citylinkc_2_PUPi, "src/CityLink/CLc_2_PUP_8bpp.png")		{tmpl_depo()}
spriteset(ss_Citylinkc_2_PDO, "src/CityLink/CLc_2_PDO_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylinkc_2_PDO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_PDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylinkc_2_PDO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLink/CLc_2_PDO_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_Citylinkc_2_PDOi, "src/CityLink/CLc_2_PDO_8bpp.png")		{tmpl_depo()}
spriteset(ss_Citylinkc_3, "src/CityLink/CLc_3_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylinkc_3, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLink/CLc_3_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylinkc_3, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLink/CLc_3_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_Citylinkc_3i, "src/CityLink/CLc_3_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_Citylinkc_1 {	loaded:  [ss_Citylinkc_1];	loading: [ss_Citylinkc_1];}
spritegroup sg_Citylinkc_2 {	loaded:  [ss_Citylinkc_2];	loading: [ss_Citylinkc_2];}
spritegroup sg_Citylinkc_3 {	loaded:  [ss_Citylinkc_3];	loading: [ss_Citylinkc_3];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_Citylinkc_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_Citylinkc;    // Alap modell
    1: return ss_Citylinkc_PDO;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_Citylinkc_2pi, current_tramtype) {
	RAIL: ss_Citylinkc_2_PDOi;
	NBAN: ss_Citylinkc_2_PDOi;
	NIAN: ss_Citylinkc_2_PDOi;
	ss_Citylinkc_2_PUPi;
}
switch (FEAT_ROADVEHS, SELF, sgw_Citylinkc_2i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_Citylinkc_2i;    // Alap modell
    1: return sgw_Citylinkc_2pi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_Citylinkci, position_in_consist ) {
    1: ss_toldat;
	2: sgw_Citylinkc_2i;
	3: ss_toldat;
	4: ss_Citylinkc_3i;
	5: ss_toldat;
    ss_Citylinkc_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_Citylinkc_2p, current_tramtype) {
	RAIL: ss_Citylinkc_2_PDO;
	NBAN: ss_Citylinkc_2_PDO;
	NIAN: ss_Citylinkc_2_PDO;
	ss_Citylinkc_2_PUP;
}
switch (FEAT_ROADVEHS, SELF, sgw_Citylinkc_2, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_Citylinkc_2;    // Alap modell
    1: return sgw_Citylinkc_2p;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_Citylinkc, position_in_consist ) {
    1: ss_toldat;
	2: sgw_Citylinkc_2;
	3: ss_toldat;
	4: sg_Citylinkc_3;
	5: ss_toldat;
    sg_Citylinkc_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_Citylinkc, (extra_callback_info1 & 0x12)) {
   0x10: sw1_Citylinkci; // Depó nézet
   0x11: sw1_Citylinkci; // Jármű infó/Refit nézet
   0x12: sw1_Citylinkci; // Jármű lista nézet
   0x00: sw1_Citylinkc; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_Citylinkc_articulated, extra_callback_info1) {
    0: item_Citylinkc;
    1: item_Citylinkc_T;
    2: item_Citylinkc_2;
    3: item_Citylinkc_T;
    4: item_Citylinkc_2;
    5: item_Citylinkc_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_Citylinkc_length, position_in_consist) {
	1: return 4;
	2: return 8;
	3: return 2;
	4: return 5;
	5: return 8;
    return 8;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_Citylinkc_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_Citylinkc_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_Citylinkc_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_Citylinkc_INFO); // Jármű infóban név
    0x20: sw_Citylinkc_namep; // Vásárlási lista név
	0x21: return string(STR_Citylinkc_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}

// Síntípus változók
/*switch (FEAT_ROADVEHS, SELF, sw_Citylinkc_effect, current_tramtype) {
	RAIL: return VISUAL_EFFECT_DIESEL;
	ELRL: return VISUAL_EFFECT_ELECTRIC;	
}*/
switch (FEAT_ROADVEHS, SELF, sw_Citylinkc_power, current_tramtype) {
	RAIL: return 1045 / 10;	// 780kW
	NBAN: return 1045 / 10;	// 780kW
	NIAN: return 1045 / 10;	// 780kW
	return 778 / 10;	// 580kW
}


item(FEAT_ROADVEHS, item_Citylinkc) {
	property {
        name:								string(STR_Citylink_NAME);
		tram_type:							RAIL;
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(2014,01,01);
        model_life:						27;
        vehicle_life:						40;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					20;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								100 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								580 kW;
        weight:							63 ton;
        cargo_capacity:					72;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
		variant_group:					item_Citylinka;
    }
    graphics {
		default:                 		sw_Citylinkc;
		purchase:						swg_Citylinkc_purchase;
		articulated_part:        		sw_Citylinkc_articulated;
		length:                  		sw_Citylinkc_length;
		cost_factor:					127 * parapuco;
		running_cost_factor:			191 * pararuco;
		additional_text: 				string(STR_Citylinkc_ADAT);
		name:							sw_Citylinkc_name;
        power:							sw_Citylinkc_power;
//		visual_effect:				sw_Citylinkc_effect;
    }
}
item(FEAT_ROADVEHS, item_Citylinkc_2) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					12;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					72;
		cargo_age_period:					0;
        sound_effect:						SOUND_POWER_STATION;
    }
    graphics {
		default:                 		sw_Citylinkc;
		length:                  		sw_Citylinkc_length;
    }
}
item(FEAT_ROADVEHS, item_Citylinkc_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_Citylinkc_length;
    }
}

// --------------------------------------------------------------------------------Citylinkc End
// --
// --
