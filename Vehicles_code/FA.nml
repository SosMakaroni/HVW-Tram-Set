// --------------------------------------------------------------------------------FA

spriteset(ss_FA_purchase, "src/BVV/FA_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_FA_P_purchase, "src/BVV/FA_BUY_P_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_FA, "src/BVV/FA_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FA, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/BVV/FA_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FA, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/BVV/FA_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FAi, "src/BVV/FA_8bpp.png")		{tmpl_depo()}
spriteset(ss_FA_PUP, "src/BVV/FA_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FA_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/BVV/FA_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FA_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/BVV/FA_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FA_PUPi, "src/BVV/FA_PUP_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_FA {	loaded:  [ss_FA];	loading: [ss_FA];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_FA_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FA_purchase;    // Alap modell
    1: return ss_FA_P_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_FAi, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FAi;    // Alap modell
    1: return ss_FA_PUPi;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_FAi, position_in_consist ) {
    1: ss_toldat;
    sgw_FAi;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_FA, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FA;    // Alap modell
    1: return ss_FA_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_FA, position_in_consist ) {
    1: ss_toldat;
    sgw_FA;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_FA, (extra_callback_info1 & 0x12)) {
   0x10: sw1_FAi; // Depó nézet
   0x11: sw1_FAi; // Jármű infó/Refit nézet
   0x12: sw1_FAi; // Jármű lista nézet
   0x00: sw1_FA; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_FA_articulated, extra_callback_info1) {
    0: item_FA;
    1: item_FA_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_FA_length, position_in_consist) {
	1: return 8;
    return 2;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_FA_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_FA_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_FA_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_FA_INFO); // Jármű infóban név
    0x20: sw_FA_namep; // Vásárlási lista név
	0x21: return string(STR_FA_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}
/*
// Lelassítás
FAtav =	(FAtav == 1) ? 2 :
			(FAtav == 2) ? 4 :
			(FAtav == 3) ? 8 :
			(FAtav == 4) ? 16 : 
			1;
*/

item(FEAT_ROADVEHS, item_FA) {
	property {
        name:								string(STR_F_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1904,01,01);
        model_life:						20;
        vehicle_life:						52;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					10;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								48 hp;
        weight:							11 ton;
        cargo_capacity:					42;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
//		variant_group:					item_FA;
    }
    graphics {
		default:                 		sw_FA;
		purchase:						swg_FA_purchase;
		articulated_part:        		sw_FA_articulated;
		length:                  		sw_FA_length;
		cost_factor:					8 * parapuco;
		running_cost_factor:			6 * pararuco;
		additional_text: 				string(STR_FA_ADAT);
		name:							sw_FA_name;
    }
}
item(FEAT_ROADVEHS, item_FA_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_FA_length;
    }
}
// --------------------------------------------------------------------------------FA End
// --
// --
