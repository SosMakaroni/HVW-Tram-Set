// --------------------------------------------------------------------------------TV

spriteset(ss_TV_purchase, "src/UV/TV_8bpp.png")		{tmpl_purchase()}  // Vásárlási jármű
spriteset(ss_TV_PDO_purchase, "src/UV/TV_PDO_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_TV_1, "src/UV/TV_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_TV_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/TV_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_TV_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/TV_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_TV_1i, "src/UV/TV_01_8bpp.png")		{tmpl_depo()}
spriteset(ss_TV_1PUP, "src/UV/TV_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_TV_1PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/TV_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_TV_1PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/TV_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_TV_1PUPi, "src/UV/TV_01_PUP_8bpp.png")		{tmpl_depo()}
spriteset(ss_TV_2, "src/UV/TV_02_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_TV_2, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/TV_02_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_TV_2, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/TV_02_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_TV_2i, "src/UV/TV_02_8bpp.png")		{tmpl_depo()}
spriteset(ss_TV_3, "src/UV/TV_03_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_TV_3, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/TV_03_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_TV_3, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/TV_03_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_TV_3i, "src/UV/TV_03_8bpp.png")		{tmpl_depo()}
spriteset(ss_TV_3PUP, "src/UV/TV_03_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_TV_3PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/TV_03_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_TV_3PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/TV_03_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_TV_3PUPi, "src/UV/TV_03_PUP_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_TV_1 {	loaded:  [ss_TV_1];	loading: [ss_TV_1];}
spritegroup sg_TV_2 {	loaded:  [ss_TV_2];	loading: [ss_TV_2];}
spritegroup sg_TV_3 {	loaded:  [ss_TV_3];	loading: [ss_TV_3];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_TV_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_TV_purchase;    // Alap modell
    1: return ss_TV_PDO_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_TV_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_TV_1i;    // Alap modell
    1: return ss_TV_1PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sgw_TV_3i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_TV_3i;    // Alap modell
    1: return ss_TV_3PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_TVi, position_in_consist ) {
    1: ss_toldat;
	2: ss_TV_2i;
	3: ss_toldat;
	4: sgw_TV_3i;
	5: ss_toldat;
    sgw_TV_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_TV_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_TV_1;    // Alap modell
    1: return ss_TV_1PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sgw_TV_3, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_TV_3;    // Alap modell
    1: return ss_TV_3PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_TV, position_in_consist ) {
    1: ss_toldat;
	2: sg_TV_2;
	3: ss_toldat;
	4: sgw_TV_3;
	5: ss_toldat;
    sgw_TV_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_TV, (extra_callback_info1 & 0x12)) {
   0x10: sw1_TVi; // Depó nézet
   0x11: sw1_TVi; // Jármű infó/Refit nézet
   0x12: sw1_TVi; // Jármű lista nézet
   0x00: sw1_TV; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_TV_articulated, extra_callback_info1) {
    0: item_TV;
    1: item_TV_T;
    2: item_TV_2;
    3: item_TV_T;
    4: item_TV_2;
    5: item_TV_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_TV_length, position_in_consist) {
	1: return 4;
	2: return 8;
	3: return 4;
	4: return 5;
	5: return 8;
    return 8;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_TV_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_TV_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_TV_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_TV_INFO); // Jármű infóban név
    0x20: sw_TV_namep; // Vásárlási lista név
	0x21: return string(STR_TV_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_TV) {
	property {
        name:								string(STR_T_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1948,01,01);
        model_life:						14;
        vehicle_life:						20;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					36;
        cost_factor:						70;
        running_cost_factor:				70;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								60 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								200 hp;
        weight:							54 ton;
        cargo_capacity:					93;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
		variant_group:					item_TM;
    }
    graphics {
		default:                 		sw_TV;
		purchase:						swg_TV_purchase;
		articulated_part:        		sw_TV_articulated;
		length:                  		sw_TV_length;
		cost_factor:					37 * parapuco;
		running_cost_factor:			27 * pararuco;
		additional_text: 				string(STR_TV_ADAT);
		name:							sw_TV_name;
    }
}
item(FEAT_ROADVEHS, item_TV_2) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					36;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					93;
		cargo_age_period:					0;
        sound_effect:						SOUND_POWER_STATION;
    }
    graphics {
		default:                 		sw_TV;
		length:                  		sw_TV_length;
    }
}
item(FEAT_ROADVEHS, item_TV_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_TV_length;
    }
}

// --------------------------------------------------------------------------------TV_03 End
// --
// --
