// --------------------------------------------------------------------------------FBPP

spriteset(ss_FBPP_purchase, "src/BVV/FBPP_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_FBPP_P_purchase, "src/BVV/FB_BUY_P_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_FBPP_1, "src/BVV/FB_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBPP_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/BVV/FB_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBPP_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/BVV/FB_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FBPP_1i, "src/BVV/FB_8bpp.png")		{tmpl_depo()}
spriteset(ss_FBPP_1_PUP, "src/BVV/FB_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBPP_1_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/BVV/FB_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBPP_1_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/BVV/FB_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FBPP_1_PUPi, "src/BVV/FB_PUP_8bpp.png")		{tmpl_depo()}
spriteset(ss_FBPP_2, "src/BVV/NP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBPP_2, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/BVV/NP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBPP_2, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/BVV/NP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FBPP_2i, "src/BVV/NP_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_FBPP_1 {	loaded:  [ss_FBPP_1];	loading: [ss_FBPP_1];}
spritegroup sg_FBPP_2 {	loaded:  [ss_FBPP_2];	loading: [ss_FBPP_2];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_FBPP_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FBPP_purchase;    // Alap modell
    1: return ss_FBPP_P_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_FBPP_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FBPP_1i;    // Alap modell
    1: return ss_FBPP_1_PUPi;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_FBPPi, position_in_consist ) {
    1: ss_toldat;
	2: ss_FBPP_2i;
	3: ss_FBPP_2i;
    sgw_FBPP_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_FBPP_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FBPP_1;    // Alap modell
    1: return ss_FBPP_1_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_FBPP, position_in_consist ) {
    1: ss_toldat;
	2: sg_FBPP_2;
	3: sg_FBPP_2;
    sgw_FBPP_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_FBPP, (extra_callback_info1 & 0x12)) {
   0x10: sw1_FBPPi; // Depó nézet
   0x11: sw1_FBPPi; // Jármű infó/Refit nézet
   0x12: sw1_FBPPi; // Jármű lista nézet
   0x00: sw1_FBPP; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_FBPP_articulated, extra_callback_info1) {
    0: item_FBPP;
    1: item_FBPP_T;
    2: item_FBPP_2;
    3: item_FBPP_2;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_FBPP_length, position_in_consist) {
	1: return 1;
	2: return 7;
	3: return 7;
    return 8;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_FBPP_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_FBPP_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_FBPP_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_FBPP_INFO); // Jármű infóban név
    0x20: sw_FBPP_namep; // Vásárlási lista név
	0x21: return string(STR_FBPP_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_FBPP) {
	property {
        name:								string(STR_F_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1956,03,01);
        model_life:						20;
        vehicle_life:						28;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					10;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								140 hp;
        weight:							25 ton;
        cargo_capacity:					40;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
		variant_group:					item_FA;
    }
    graphics {
		default:                 		sw_FBPP;
		purchase:						swg_FBPP_purchase;
		articulated_part:        		sw_FBPP_articulated;
		length:                  		sw_FBPP_length;
		cost_factor:					20 * parapuco;
		running_cost_factor:			17 * pararuco;
		additional_text: 				string(STR_FBPP_ADAT);
		name:							sw_FBPP_name;
    }
}
item(FEAT_ROADVEHS, item_FBPP_2) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					10;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					42;
		cargo_age_period:					0;
        sound_effect:						SOUND_POWER_STATION;
    }
    graphics {
		default:                 		sw_FBPP;
		length:                  		sw_FBPP_length;
    }
}
item(FEAT_ROADVEHS, item_FBPP_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_FBPP_length;
    }
}
// --------------------------------------------------------------------------------FBPP End
// --
// --
