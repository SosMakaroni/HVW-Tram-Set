// --------------------------------------------------------------------------------T5C51a

spriteset(ss_T5C51a_purchase, "src/T5C5/T5C5a_01_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_T5C51a_P_purchase, "src/T5C5/T5C5a_01_BUY_P_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_T5C51a_1, "src/T5C5/T5C5a_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_T5C51a_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/T5C5/T5C5a_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_T5C51a_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/T5C5/T5C5a_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_T5C51a_1i, "src/T5C5/T5C5a_01_8bpp.png")		{tmpl_depo()}
spriteset(ss_T5C51a_1_PUP, "src/T5C5/T5C5a_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_T5C51a_1_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/T5C5/T5C5a_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_T5C51a_1_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/T5C5/T5C5a_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_T5C51a_1_PUPi, "src/T5C5/T5C5a_01_PUP_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_T5C51a_1 {	loaded:  [ss_T5C51a_1];	loading: [ss_T5C51a_1];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_T5C51a_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_T5C51a_purchase;    // Alap modell
    1: return ss_T5C51a_P_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_T5C51a_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_T5C51a_1i;    // Alap modell
    1: return ss_T5C51a_1_PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_T5C51a_1i, position_in_consist ) {
    1: ss_toldat;
    sgw_T5C51a_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_T5C51a_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_T5C51a_1;    // Alap modell
    1: return ss_T5C51a_1_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_T5C51a_1, position_in_consist ) {
    1: ss_toldat;
    sgw_T5C51a_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_T5C51a, (extra_callback_info1 & 0x12)) {
   0x10: sw1_T5C51a_1i; // Depó nézet
   0x11: sw1_T5C51a_1i; // Jármű infó/Refit nézet
   0x12: sw1_T5C51a_1i; // Jármű lista nézet
   0x00: sw1_T5C51a_1; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_T5C51a_articulated, extra_callback_info1) {
    0: item_T5C51a;
    1: item_T5C51a_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_T5C51a_length, position_in_consist) {
	1: return 8;
    return 7;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_T5C51a_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_T5C51a_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_T5C51a_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_T5C51a_INFO); // Jármű infóban név
    0x20: sw_T5C51a_namep; // Vásárlási lista név
	0x21: return string(STR_T5C51a_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_T5C51a) {
	property {
        name:								string(STR_T5C5_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1980,02,01);
        model_life:						25;
        vehicle_life:						50;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					30;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								65 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								180 kW;
        weight:							19 ton;
        cargo_capacity:					100;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
		variant_group:					item_T5C51b;
    }
    graphics {
		default:                 		sw_T5C51a;
		purchase:						swg_T5C51a_purchase;
		articulated_part:        		sw_T5C51a_articulated;
		length:                  		sw_T5C51a_length;
		cost_factor:					22 * parapuco;
		running_cost_factor:			27 * pararuco;
		additional_text: 				string(STR_T5C51a_ADAT);
		name:							sw_T5C51a_name;
    }
}
item(FEAT_ROADVEHS, item_T5C51a_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_T5C51a_length;
    }
}

// --------------------------------------------------------------------------------T5C51a End
// --
// --
