// --------------------------------------------------------------------------------COM5bernab

spriteset(ss_COM5bernab_BUY, "src/SIEMENS/BERN/COM5_bernab_BUY_8bpp.png")		{tmpl_purchase()}  // Vásárlási jármű
spriteset(ss_COM5bernab_BUY_P, "src/SIEMENS/BERN/COM5_berna_BUY_P_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő*/

// Rakodási grafika

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, sw_COM5bernab_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 8 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_COM5bernab_BUY;    // Alap modell
    1: return ss_COM5bernab_BUY_P;    // Alap modell
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sw_COMLp_bernb_info, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 8 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_COMLp_bernb_info;    // Alap modell
    1: return ss_COMLp_berna_PUP_info;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw_COM5bernab_info, position_in_consist ) {
	1: sw_COMLp_bernb_info;
	2: ss_COMM_bernba_info;
	3: ss_COML_bernb_info;
	4: ss_COMR_bernab_info;
    ss_COMF_bernab_info;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sw_COMLp_bernb, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 8 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_COMLp_bernb;    // Alap modell
    1: return ss_COMLp_berna_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_COM5bernab, position_in_consist ) {
	1: sw_COMLp_bernb;
	2: ss_COMM_bernba;
	3: ss_COML_bernb;
	4: ss_COMR_bernab;
    ss_COMF_bernab;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_COM5bernab, (extra_callback_info1 & 0x12)) {
   0x10: sw_COM5bernab_info; // Depó nézet
   0x11: sw_COM5bernab_info; // Jármű infó/Refit nézet
   0x12: sw_COM5bernab_info; // Jármű lista nézet
   0x00: sw1_COM5bernab; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_COM5bernab_articulated, extra_callback_info1) {
	0: item_COM5bernab;
	1: item_COM_long;
	2: item_COM_middle;
	3: item_COM_long;
	4: item_COM_rear;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_COM5bernab_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_COM5bernab_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_COM5bernab_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_COM5bernab_INFO); // Jármű infóban név
    0x20: sw_COM5bernab_namep; // Vásárlási lista név
	0x21: return string(STR_COM_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_COM5bernab) {
	property {
        name:							string(STR_COM_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(2002,03,1);
        model_life:						20;
        vehicle_life:					40;
        reliability_decay:				18;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					12;
        sprite_id:						SPRITE_ID_NEW_ROADVEH;
        speed:							70 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:							400 kW;
        weight:							36 ton;
        cargo_capacity:					36;
		cargo_age_period:				185;
        sound_effect:					SOUND_POWER_STATION;
		length:                  		6;
		variant_group:					item_COM5potsa;
    }
    graphics {
		default:                 		sw_COM5bernab;
		purchase:						sw_COM5bernab_purchase;
		articulated_part:        		sw_COM5bernab_articulated;
		cost_factor:					45 * parapuco;
		running_cost_factor:			76 * pararuco;
		additional_text: 				string(STR_COM5bernab_ADAT);
		name:							sw_COM5bernab_name;
    }
	livery_override (item_COM_long ) {
		default:						sw_COM5bernab;
	}
	livery_override (item_COM_middle ) {
		default:						sw_COM5bernab;
	}
	livery_override (item_COM_rear ) {
		default:						sw_COM5bernab;
	}

}

// --------------------------------------------------------------------------------COM5bernab End
// --
// --
