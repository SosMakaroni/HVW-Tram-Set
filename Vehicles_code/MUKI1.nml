// --------------------------------------------------------------------------------MUKI1

spriteset(ss_MUKI1_purchase, "src/MUKI/MUKI1_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_MUKI1_PDO_purchase, "src/MUKI/MUKI1_PDO_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_MUKI1, "src/MUKI/MUKI11_1x_8bpp.png")		{tmpl_1(1)} //Motorkocsi
alternative_sprites(ss_MUKI1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MUKI1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MUKI1i, "src/MUKI/MUKI11_8bpp.png")		{tmpl_depo()}
spriteset(ss_MUKI1_PUP, "src/MUKI/MUKI1_PUP_1x_8bpp.png")		{tmpl_1(1)} //Áramszedő
alternative_sprites(ss_MUKI1_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MUKI/MUKI1_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MUKI1_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MUKI/MUKI1_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MUKI1_PUPi, "src/MUKI/MUKI1_PUP_8bpp.png")		{tmpl_depo()}
spriteset(ss_MUKI1_URES, "src/MUKI/MUKI11_RAK_URES_1x_8bpp.png")		{tmpl_1(1)} //Motorkocsi nyitva
alternative_sprites(ss_MUKI1_URES, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_URES_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MUKI1_URES, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_URES_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MUKI1_URESi, "src/MUKI/MUKI11_RAK_URES_8bpp.png")		{tmpl_depo()}
spriteset(ss_MUKI1_DOBOZ, "src/MUKI/MUKI11_RAK_LADA_1x_8bpp.png")		{tmpl_1(1)} //Motorkocsi doboz
alternative_sprites(ss_MUKI1_DOBOZ, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_LADA_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MUKI1_DOBOZ, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_LADA_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MUKI1_DOBOZi, "src/MUKI/MUKI11_RAK_LADA_8bpp.png")		{tmpl_depo()}
spriteset(ss_MUKI1_ARAMSZEDO, "src/MUKI/MUKI11_RAK_ARAMSZEDO_1x_8bpp.png")		{tmpl_1(1)} //Motorkocsi ARAMSZEDO
alternative_sprites(ss_MUKI1_ARAMSZEDO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_ARAMSZEDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_MUKI1_ARAMSZEDO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/MUKI/MUKI11_RAK_ARAMSZEDO_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_MUKI1_ARAMSZEDOi, "src/MUKI/MUKI11_RAK_ARAMSZEDO_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_MUKI1_DOBOZ {	loaded:  [ss_MUKI1];	loading: [ss_MUKI1_URES, ss_MUKI1_DOBOZ, ss_MUKI1_DOBOZ];}
spritegroup sg_MUKI1_ARAMSZEDO {	loaded:  [ss_MUKI1];	loading: [ss_MUKI1_URES, ss_MUKI1_ARAMSZEDO, ss_MUKI1_ARAMSZEDO];}

spritegroup sg_MUKI1_DOBOZi {	loaded:  [ss_MUKI1i];	loading: [ss_MUKI1_URESi, ss_MUKI1_DOBOZi, ss_MUKI1_DOBOZi];}
spritegroup sg_MUKI1_ARAMSZEDOi {	loaded:  [ss_MUKI1i];	loading: [ss_MUKI1_URESi, ss_MUKI1_ARAMSZEDOi, ss_MUKI1_ARAMSZEDOi];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_MUKI1_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_MUKI1_purchase;    // Alap modell
    1: return ss_MUKI1_PDO_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sw2_MUKI1_1i, cargo_type_in_veh) {
	ENSP: return sg_MUKI1_ARAMSZEDOi;
	sg_MUKI1_DOBOZi;
}
switch (FEAT_ROADVEHS, SELF, swg_MUKI1_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sw2_MUKI1_1i;    // Alap modell
    1: return ss_MUKI1_PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_MUKI1i, position_in_consist ) {
	1: ss_toldat;
    swg_MUKI1_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sw2_MUKI1_1, cargo_type_in_veh) {
	ENSP: return sg_MUKI1_ARAMSZEDO;
	sg_MUKI1_DOBOZ;
}
switch (FEAT_ROADVEHS, SELF, swg_MUKI1_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sw2_MUKI1_1;    // Alap modell
    1: return ss_MUKI1_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sw1_MUKI1, position_in_consist ) {
	1: ss_toldat;
    swg_MUKI1_1;
}
// Rajz
switch(FEAT_ROADVEHS, SELF, sw_MUKI1, (extra_callback_info1 & 0x12)) {
   0x10: sw1_MUKI1i; // Depó nézet
   0x11: sw1_MUKI1i; // Jármű infó/Refit nézet
   0x12: sw1_MUKI1i; // Jármű lista nézet
   0x00: sw1_MUKI1; // Játék nézet
}


// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_MUKI1_articulated, extra_callback_info1) {
    0: item_MUKI1;
    1: item_MUKI1_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_MUKI1_length, position_in_consist) {
	1: return 8;
    return 2;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_MUKI1_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_MUKI1_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_MUKI1_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_MUKI1_INFO); // Jármű infóban név
    0x20: sw_MUKI1_namep; // Vásárlási lista név
	0x21: return string(STR_MUKI1_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_MUKI1_livery_text, cargo_type_in_veh) {
	ENSP: return string(STR_COLOUR_SCHEME_PANTO);
	return string(STR_COLOUR_SCHEME_LADA);
}

//Kapacitás

item(FEAT_ROADVEHS, item_MUKI1) {
	property {
        name:								string(STR_MUKI_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1926,01,01);
        model_life:						52;
        vehicle_life:						50;
        reliability_decay:				20;
		cargo_allow_refit:       			[STEL, ENSP, WOOD];
		cargo_disallow_refit:			[LVST];
        refittable_cargo_classes:		bitmask(CC_PIECE_GOODS);
        non_refittable_cargo_classes:	bitmask(CC_PASSENGERS, CC_EXPRESS, CC_REFRIGERATED, CC_BULK);
        loading_speed:					4;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								110 hp;
        weight:							10 ton;
        cargo_capacity:					4;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
// 		variant_group:					item_MUKI1;
   }
    graphics {
		default:                 		sw_MUKI1;
		purchase:						swg_MUKI1_purchase;
		articulated_part:        		sw_MUKI1_articulated;
		length:                  		sw_MUKI1_length;
		cargo_subtype_text: 			sw_MUKI1_livery_text;
		cargo_capacity: 				4 * paracapa;
		purchase_cargo_capacity:		4 * paracapa;
		cost_factor:					4 * parapuco;
		running_cost_factor:			11 * pararuco;
		additional_text: 				string(STR_MUKI1_ADAT);
		name:							sw_MUKI1_name;
    }
}
item(FEAT_ROADVEHS, item_MUKI1_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(NO_CLIMATE);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_MUKI1_length;
    }
}

// --------------------------------------------------------------------------------MUKI1 End

