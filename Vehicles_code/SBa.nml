// --------------------------------------------------------------------------------SBa

spriteset(ss_SBa_purchase, "src/GANZBUS/SBa_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_SBa_P_purchase, "src/GANZBUS/SBa_P_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_SBa, "src/GANZBUS/SBa_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_SBa, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/GANZBUS/SBa_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_SBa, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/GANZBUS/SBa_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_SBai, "src/GANZBUS/SBa_8bpp.png")		{tmpl_depo()}
spriteset(ss_SBa_PUP, "src/GANZBUS/SBa_P_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_SBa_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/GANZBUS/SBa_P_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_SBa_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/GANZBUS/SBa_P_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_SBa_PUPi, "src/GANZBUS/SBa_P_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_SBa {	loaded:  [ss_SBa];	loading: [ss_SBa];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_SBa_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_SBa_purchase;    // Alap modell
    1: return ss_SBa_P_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, sgw_SBai, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_SBai;    // Alap modell
    1: return ss_SBa_PUPi;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_SBai, position_in_consist ) {
    1: ss_toldat;
    sgw_SBai;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, sgw_SBa, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_SBa;    // Alap modell
    1: return ss_SBa_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw1_SBa, position_in_consist ) {
    1: ss_toldat;
    sgw_SBa;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_SBa, (extra_callback_info1 & 0x12)) {
   0x10: sw1_SBai; // Depó nézet
   0x11: sw1_SBai; // Jármű infó/Refit nézet
   0x12: sw1_SBai; // Jármű lista nézet
   0x00: sw1_SBa; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_SBa_articulated, extra_callback_info1) {
    0: item_SBa;
    1: item_SBa_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_SBa_length, position_in_consist) {
	1: return 8;
    return 2;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_SBa_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_SBa_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_SBa_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_SBa_INFO); // Jármű infóban név
    0x20: sw_SBa_namep; // Vásárlási lista név
	0x21: return string(STR_SBa_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_SBa) {
	property {
        name:								string(STR_SB_NAME);
//		tram_type:							RAIL;
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1927,01,01);
        model_life:						10;
        vehicle_life:						14;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					12;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								45 hp;
        weight:							11 ton;
        cargo_capacity:					32;
		cargo_age_period:					185;
        sound_effect:						SOUND_DEPARTURE_OLD_BUS;
		visual_effect:					visual_effect(VISUAL_EFFECT_DIESEL, 2);
//		variant_group:					item_SBa;
    }
    graphics {
		default:                 		sw_SBa;
		purchase:						swg_SBa_purchase;
		articulated_part:        		sw_SBa_articulated;
		length:                  		sw_SBa_length;
		cost_factor:					8 * parapuco;
		running_cost_factor:			6 * pararuco;
		additional_text: 				string(STR_SBa_ADAT);
		name:							sw_SBa_name;
    }
}
item(FEAT_ROADVEHS, item_SBa_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_SBa_length;
    }
}
// --------------------------------------------------------------------------------SBa End
// --
// --
