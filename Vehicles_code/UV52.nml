// --------------------------------------------------------------------------------UV52

spriteset(ss_UV52_purchase, "src/UV/UV52_BUY_8bpp.png")		{tmpl_purchase()} // Vásárlási jármű
spriteset(ss_UV52_PDO_purchase, "src/UV/UV52_BUY_P_8bpp.png")		{tmpl_purchase()} // Vásárlási áramszedő

spriteset(ss_UV52_1, "src/UV/UV1_01_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV52_1, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_01_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV52_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_01_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV52_1i, "src/UV/UV1_01_8bpp.png")		{tmpl_depo()}
spriteset(ss_UV52_1_PUP, "src/UV/UV1_01_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV52_1_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_01_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV52_1_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_01_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV52_1_PUPi, "src/UV/UV1_01_PUP_8bpp.png")		{tmpl_depo()}
spriteset(ss_UV52_2, "src/UV/UV1_02_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV52_2, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_02_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV52_2, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_02_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV52_2i, "src/UV/UV1_02_8bpp.png")		{tmpl_depo()}
spriteset(ss_UV52_2_PDO, "src/UV/UV1_02_PDO_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_UV52_2_PDO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/UV/UV1_02_PDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_UV52_2_PDO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/UV/UV1_02_PDO_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_UV52_2_PDOi, "src/UV/UV1_02_PDO_8bpp.png")		{tmpl_depo()}

// Rakodási grafika
spritegroup sg_UV52_1 {	loaded:  [ss_UV52_1];	loading: [ss_UV52_1];}
spritegroup sg_UV52_2 {	loaded:  [ss_UV52_2];	loading: [ss_UV52_2];}

// Vásárlási grafika
switch (FEAT_ROADVEHS, SELF, swg_UV52_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV52_purchase;    // Alap modell
    1: return ss_UV52_PDO_purchase;    // Pantonograf (le)
}

// Lista grafika
switch (FEAT_ROADVEHS, SELF, swg_UV52_1i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV52_1i;    // Alap modell
    1: return ss_UV52_1_PUPi;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, swg_UV52_2i, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_UV52_2i;    // Alap modell
    1: return ss_UV52_2_PDOi;    // Pantonograf (le)
}

switch (FEAT_ROADVEHS, SELF, sw1_UV52i, position_in_consist ) {
    1: ss_toldat;
	2: swg_UV52_2i;
	3: ss_toldat;
    swg_UV52_1i;
}

// Játék grafika
switch (FEAT_ROADVEHS, SELF, swg_UV52_1, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV52_1;    // Alap modell
    1: return ss_UV52_1_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, swg_UV52_2, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_UV52_2;    // Alap modell
    1: return ss_UV52_2_PDO;    // Pantonograf (le)
}

switch (FEAT_ROADVEHS, SELF, sw1_UV52, position_in_consist ) {
    1: ss_toldat;
	2: swg_UV52_2;
	3: ss_toldat;
    swg_UV52_1;
}

// Rajz
switch(FEAT_ROADVEHS, SELF, sw_UV52, (extra_callback_info1 & 0x12)) {
   0x10: sw1_UV52i; // Depó nézet
   0x11: sw1_UV52i; // Jármű infó/Refit nézet
   0x12: sw1_UV52i; // Jármű lista nézet
   0x00: sw1_UV52; // Játék nézet
}

// Csuklosítás
switch (FEAT_ROADVEHS, SELF, sw_UV52_articulated, extra_callback_info1) {
    0: item_UV52;
    1: item_UV52_T;
    2: item_UV52_2;
    3: item_UV52_T;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Modelhossz
switch (FEAT_ROADVEHS, SELF, sw_UV52_length, position_in_consist) {
	1: return 4;
	2: return 5;
	3: return 8;
    return 8;
}

// Szövegek
switch(FEAT_ROADVEHS, SELF, sw_UV52_namep, (extra_callback_info1 >> 8) & 0xFFFF) {
    1: return string(STR_UV52_BUY);
	return CB_RESULT_NO_TEXT;
}
switch(FEAT_ROADVEHS, SELF, sw_UV52_name, extra_callback_info1 & 0xFF) {
    0x11: return string(STR_UV52_INFO); // Jármű infóban név
    0x20: sw_UV52_namep; // Vásárlási lista név
	0x21: return string(STR_UV52_NAME); // Elővásárlási név
	return CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_UV52) {
	property {
        name:								string(STR_UV52_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1962,01,01);
        model_life:						25;
        vehicle_life:						45;
        reliability_decay:				20;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					34;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								60 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								296 kW;
        weight:							40 ton;
        cargo_capacity:					94;
		cargo_age_period:					185;
        sound_effect:						SOUND_POWER_STATION;
		variant_group:					item_UV11;
    }
    graphics {
		default:                 		sw_UV52;
		purchase:						swg_UV52_purchase;
		articulated_part:        		sw_UV52_articulated;
		length:                  		sw_UV52_length;
		cost_factor:					40 * parapuco;
		running_cost_factor:			45 * pararuco;
		additional_text: 				string(STR_UV52_ADAT);
		name:							sw_UV52_name;
    }
}
item(FEAT_ROADVEHS, item_UV52_2) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					34;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					94;
		cargo_age_period:					0;
        sound_effect:						SOUND_POWER_STATION;
    }
    graphics {
		default:                 		sw_UV52;
		length:                  		sw_UV52_length;
    }
}
item(FEAT_ROADVEHS, item_UV52_T) {
	property {
        name:								string(STR_BUG);
        climates_available:				bitmask(CLIMATE_TOYLAND);
        introduction_date:				date(4999999,01,01);
        refittable_cargo_classes:		bitmask(NO_CARGO_CLASS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					0;
        cost_factor:						0;
        running_cost_factor:				0;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_NONE;
        power:								0 kW;
        weight:							0 ton;
        cargo_capacity:					0;
		cargo_age_period:					0;
    }
    graphics {
		default:                 		ss_toldat;
		length:                  		sw_UV52_length;
    }
}

// --------------------------------------------------------------------------------UV52_02 End
// --
// --
